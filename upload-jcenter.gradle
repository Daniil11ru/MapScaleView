/*
deps to build.gradle

classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

./gradlew bintrayUpload
 */

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = GROUP
version = VERSION_NAME

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : System.getenv("bintrayUser")
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : System.getenv("bintrayApiKey")
    pkg {
        repo = BINTRAY_REPO
        name = POM_ARTIFACT_ID
        licenses = [BINTRAY_LICENSE]
        vcsUrl = POM_SCM_URL
        websiteUrl = POM_URL
    }
    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = VERSION_NAME

        pom.project {
            name POM_NAME
            packaging POM_PACKAGING
            description POM_DESCRIPTION
            url POM_URL

            scm {
                url POM_SCM_URL
                connection POM_SCM_CONNECTION
                developerConnection POM_SCM_DEV_CONNECTION
            }

            licenses {
                license {
                    name POM_LICENCE_NAME
                    url POM_LICENCE_URL
                    distribution POM_LICENCE_DIST
                }
            }

            developers {
                developer {
                    id POM_DEVELOPER_ID
                    name POM_DEVELOPER_NAME
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
    configurations.compileOnly.setCanBeResolved(true)
    classpath += configurations.compileOnly
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    from javadocs.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}